apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 34
    defaultConfig {
        applicationId "info.hannes.floatingView.sample"
        minSdkVersion 16
        targetSdkVersion 34
        versionCode getGitCommitCount()
        versionName getVersion()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    lint {
        disable 'MissingTranslation'
    }
    namespace 'info.hannes.floatingView.sample'

}

dependencies {
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation project(':library')
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}

static def getLatestGitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.toString().trim()
}

static def getGitCommitCount() {
    def process = "git rev-list HEAD --count".execute()
    return process.text.toInteger() + 2109
}

static def getTag() {
    def process = "date +%y-%m".execute()
    def version = process.text.toString().trim()
    return version + "." + getGitCommitCount()
}

static def getVersion() {
    return getTag() + "-" + getLatestGitHash()
}
